---
# Copyright (c) 2024 HashiCorp, Inc.
# Terraform and the Terraform logo are trademarks of HashiCorp.
#
# Documentation licensed under the Business Source License, Version 1.1.
# The original work was translated from English into Brazilian Portuguese.
# https://github.com/hashicorp/terraform/blob/-/LICENSE

source_url: https://github.com/hashicorp/web-unified-docs/blob/main/content/terraform/v1.14.x/docs/language/index.mdx
revision: 110cc70f5ac401444bfe089dd5caa2f39038e2d7
status: ready

page_title: Visão geral - Linguagem de configuração
description: >-
  Use a linguagem de configuração do Terraform para descrever a infraestrutura
  que o Terraform gerencia.
---

# Documentação da linguagem do Terraform

Esta é a documentação da linguagem de configuração do Terraform.
Ela é relevante para pessoas usuárias do [Terraform CLI](/terraform/cli),
[HCP Terraform](https://cloud.hashicorp.com/products/terraform) e
[Terraform Enterprise](/terraform/enterprise).
A linguagem do Terraform é sua principal interface de pessoa usuária.
Os arquivos de configuração que você escreve na linguagem do Terraform informam
ao Terraform quais plugins instalar, qual infraestrutura criar e quais dados
buscar.
A linguagem do Terraform também permite definir dependências entre recursos e
criar vários recursos semelhantes a partir de um único bloco de configuração.

> **Prática:** Faça os tutoriais
> [Escreva configuração do Terraform](/terraform/tutorials/configuration-language).

## Sobre a linguagem Terraform

O principal objetivo da linguagem Terraform é declarar
[recursos](/terraform/language/resources), que representam objetos de
infraestrutura.
Todos os outros recursos da linguagem existem apenas para tornar a definição de
recursos mais flexível e conveniente.

Uma _configuração Terraform_ é um documento completo na linguagem Terraform que
informa ao Terraform como gerenciar uma determinada coleção de infraestrutura.
Uma configuração pode consistir em vários arquivos e diretórios.

A sintaxe da linguagem Terraform consiste em apenas alguns elementos básicos:

```hcl
resource "aws_vpc" "main" {
  cidr_block = var.base_cidr_block
}

<TIPO DE BLOCO> "<RÓTULO DO BLOCO>" "<RÓTULO DO BLOCO>" {
  # Corpo do bloco
  <IDENTIFICADOR> = <EXPRESSÃO> # Argumento
}
```

- _Blocos_ são contêineres para outros conteúdos e representam geralmente a
  configuração de algum tipo de objeto, como um recurso.
  Os blocos têm um _tipo de bloco_, podem ter zero ou mais _rótulos_ e têm um
  _corpo_ que contém qualquer número de argumentos e blocos aninhados.
  A maioria dos recursos do Terraform é controlada por blocos de nível superior
  em um arquivo de configuração.
- _Argumentos_ atribuem um valor a um nome.
  Eles aparecem dentro de blocos.
- _Expressões_ representam um valor, seja literalmente ou por meio de referência
  e combinação de outros valores.
  Elas aparecem como valores para argumentos ou dentro de outras expressões.

A linguagem Terraform é declarativa, descrevendo um objetivo pretendido em vez
dos passos para atingir esse objetivo.
A ordem dos blocos e dos arquivos nos quais eles são organizados geralmente não
é significativa; o Terraform considera apenas relacionamentos implícitos e
explícitos entre recursos ao determinar uma ordem de operações.

### Exemplo

O exemplo a seguir descreve uma topologia de rede simples para a Amazon Web
Services, apenas para dar uma ideia da estrutura e da sintaxe da linguagem
Terraform.
Configurações semelhantes podem ser criadas para outros serviços de rede
virtual, usando tipos de recursos definidos por outros provedores, e uma
configuração de rede prática geralmente conterá elementos adicionais não
mostrados aqui.

```hcl
terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 1.0.4"
    }
  }
}

variable "aws_region" {}

variable "base_cidr_block" {
  description = "Uma definição de intervalo CIDR /16, como 10.1.0.0/16, que a VPC utilizará."
  default = "10.1.0.0/16"
}

variable "availability_zones" {
  description = "Uma lista de zonas de disponibilidade nas quais criar sub-redes."
  type = list(string)
}

provider "aws" {
  region = var.aws_region
}

resource "aws_vpc" "main" {
  # Referenciar a variável base_cidr_block permite que o endereço de rede seja
  # alterado sem modificar a configuração.
  cidr_block = var.base_cidr_block
}

resource "aws_subnet" "az" {
  # Cria uma sub-rede para cada zona de disponibilidade especificada.
  count = length(var.availability_zones)

  # Para cada sub-rede, usa uma das zonas de disponibilidade especificadas.
  availability_zone = var.availability_zones[count.index]

  # Ao referenciar o objeto aws_vpc.main, o Terraform sabe que a sub-rede deve
  # ser criada somente após a criação da VPC.
  vpc_id = aws_vpc.main.id

  # Funções e operadores integrados podem ser usados para transformações simples
  # de valores, como calcular um endereço de sub-rede.
  # Aqui, criamos um prefixo /20 para cada sub-rede, usando endereços
  # consecutivos para cada zona de disponibilidade, como 10.1.16.0/20.
  cidr_block = cidrsubnet(aws_vpc.main.cidr_block, 4, count.index+1)
}
```
